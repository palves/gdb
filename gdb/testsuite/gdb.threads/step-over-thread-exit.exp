# Copyright 2021 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Test stepping over a breakpoint installed on an instruction that
# exits the thread.

standard_testfile .c

set syscalls_src $srcdir/lib/my-syscalls.S

if { [build_executable "failed to prepare" $testfile \
	  [list $srcfile $syscalls_src] {debug pthreads}] == -1 } {
    return
}

proc test {displaced-stepping target-non-stop schedlock} {
    save_vars ::GDBFLAGS {
	append ::GDBFLAGS " -ex \"maintenance set target-non-stop ${target-non-stop}\""
	clean_restart $::binfile
    }

    gdb_test_no_output "set displaced-stepping ${displaced-stepping}"

    if { ![runto_main] } {
	return
    }

    gdb_breakpoint "my_exit_syscall"

    gdb_test_no_output "set scheduler-locking ${schedlock}"

    if {$schedlock} {
	gdb_test "continue" \
	    "hit Breakpoint $::decimal.* my_exit_syscall .*" \
	    "continue until syscall"

	gdb_test "continue" \
	    "No unwaited-for children left." \
	    "continue stops when thread exits"
    } else {
	for { set i 0 } { $i < 100 } { incr i } {
	    with_test_prefix "iter $i" {
		if { [gdb_test "continue" "hit Breakpoint $::decimal.* my_exit_syscall .*"] != 0 } {
		    # Exit if there's a failure to avoid lengthy timeouts.
		    break
		}
	    }
	}
    }
}

foreach_with_prefix displaced-stepping {off auto} {
    foreach_with_prefix target-non-stop {off on} {
	foreach_with_prefix schedlock {off on} {
	    test ${displaced-stepping} ${target-non-stop} ${schedlock}
	}
    }
}
