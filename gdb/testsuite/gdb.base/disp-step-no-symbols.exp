# Copyright (C) 2015 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

standard_testfile start.c

if { ![support_displaced_stepping] } {
    unsupported "displaced stepping"
    return -1
}

if { [build_executable "failed to build" ${testfile} $srcfile] } {
    return -1
}

# Get the current PC.  MSG is used as test message.

proc get_pc { msg } {
    global hex gdb_prompt

    set addr ""
    gdb_test_multiple "p /x \$pc" "$msg" {
	-re " = ($hex).*$gdb_prompt $" {
	    set addr $expect_out(1,string)
	    pass "$msg"
	}
    }

    return $addr
}

proc test {displaced} {
    global hex
    global binfile

    clean_restart $binfile

    if ![runto_main] {
	fail "couldn't run to main"
	untested "stepping over breakpoint with displaced=$displaced"
	return -1
    }

    delete_breakpoints

    set msg "purging symbols"
    gdb_test_multiple "symbol-file" "$msg" {
	-re "Discard symbol table.*y or n. $" {
	    gdb_test "y" "No symbol file now." "$msg"
	}
    }

    set before_addr [get_pc "get before PC"]

    gdb_test "break *\$pc" "Breakpoint .* at $hex"

    gdb_test_no_output "set displaced-stepping $displaced"

    gdb_test "stepi" "$hex in \?\? .*"

    set after_addr [get_pc "get after PC"]

    gdb_assert {$before_addr != $after_addr} "advanced"
}

foreach displaced { "off" "on" "auto" } {
    with_test_prefix "displaced=$displaced" {
	test $displaced
    }
}
